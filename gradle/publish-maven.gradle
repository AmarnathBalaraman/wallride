apply plugin: "propdeps-maven"

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->
        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // sort to make pom dependencies order consistent to ease comparison of older poms
        generatedPom.dependencies = generatedPom.dependencies.sort { dep ->
            "$dep.scope:$dep.groupId:$dep.artifactId"
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.description
            description = gradleProject.description
            url = "http://wallride.org/"
            organization {
                name = "Tagbangers, Inc."
                url = "https://www.tagbangers.co.jp/"
            }
            licenses {
                license {
                    name "The Apache Software License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                }
            }
            scm {
                url = "https://github.com/tagbangers/wallride"
            }
            developers {
                developer {
                    name = "OGAWA, Takeshi"
                    email = "ogawa@tagbangers.co.jp"
                    timezone = "-9"
                }
                developer {
                    name = "SASAKI, Alisa"
                    email = "sasaki@tagbangers.co.jp"
                    timezone = "-9"
                }
            }
            issueManagement {
                system = "Jira"
                url = "https://jira.springsource.org/browse/SPR"
            }
        }
    }
}