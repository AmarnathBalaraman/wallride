configurations.archives.artifacts.clear()
artifacts {
    // work around GRADLE-2406 by attaching text artifact
    archives(file("wallride-parent.txt"))
}

dependencies {
    compile project(':wallride-server')

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile('org.springframework.boot:spring-boot-starter-thymeleaf') {
        exclude group: 'org.thymeleaf', module: 'thymeleaf-spring4'
        exclude group: 'nz.net.ultraq.thymeleaf', module: 'thymeleaf-layout-dialect'
    }
    compile 'org.springframework.boot:spring-boot-starter-batch'
    compile 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    generatedPomCustomization.enabled = false
}

install {
    repositories.mavenInstaller.pom.whenConfigured {
        packaging = 'pom'
        project {
            parent {
                groupId "org.springframework.boot"
                artifactId "spring-boot-starter-parent"
                version project.ext['spring-boot.version']
            }
        }
        withXml {
            asNode().children().last() + {
                resolveStrategy = Closure.DELEGATE_FIRST
                properties {
                    project.ext.properties.each { key, value ->
                        if (key.endsWith(".version")) {
                            "${key}" value
                        }
                    }
                }
            }
        }
    }
}